buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
}
configurations {
    smallicu
}
dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    // Note: you can just replace "smallicu" by compile
    // and remove the "compile files" part if you do not
    // care about the size of the apk
    smallicu 'com.ibm.icu:icu4j:57.1'
    compile files("$buildDir/icu4j-57.1.small.jar") {
        builtBy 'makesmallicu'
    }
}

task makesmallicu (type:Exec, dependsOn: configurations.smallicu) {
    inputs.file "mksmallicu.sh"
    inputs.file "${configurations.smallicu.resolve()[0]}"
    outputs.dir "$buildDir"
    outputs.file "$buildDir/icu4j-57.1.small.jar"
    // necessary due to gradle bug: GRADLE-2638
    new File("$buildDir").mkdirs()
    commandLine "./mksmallicu.sh", "${configurations.smallicu.resolve()[0]}", "$buildDir/icu4j-57.1.small.jar"
}

def utildir = hasProperty("quickdicUtilDir") ? quickdicUtilDir : "Util"

android {
    compileSdkVersion 25
    buildToolsVersion "27.0.1"
    lintOptions {
        disable "MissingTranslation"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', utildir + "/src"]
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    defaultConfig {
        applicationId 'de.reimardoeffinger.quickdic'
        targetSdkVersion 25
        resConfigs "de", "es", "fr", "it", "ja", "pt", "pt-rBR", "ru"
    }
}

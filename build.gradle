buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.2'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    google()
}
configurations {
    smallicu
}
dependencies {
    def core_version = "1.12.0"

    implementation "androidx.core:core:$core_version"
    implementation "androidx.preference:preference:1.2.1"
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation "com.google.android.material:material:1.10.0"
    // Note: you can just replace "smallicu" by implementation
    // and remove the "compile files" part if you do not
    // care about the size of the apk
    smallicu 'com.ibm.icu:icu4j:57.2'
    implementation files("$buildDir/icu4j-57.2.small.jar") {
        builtBy 'makesmallicu'
    }
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

task makesmallicu (type:Exec, dependsOn: configurations.smallicu) {
    inputs.file "mksmallicu.sh"
    inputs.file "${configurations.smallicu.resolve()[0]}"
    outputs.file "$buildDir/icu4j-57.2.small.jar"
    commandLine "./mksmallicu.sh", "${configurations.smallicu.resolve()[0]}", "$buildDir/icu4j-57.2.small.jar"
}

def utildir = hasProperty("quickdicUtilDir") ? quickdicUtilDir : "Util"

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

android {
    compileSdk 34
    namespace "com.hughes.android.dictionary"
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', utildir + "/src"]
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    defaultConfig {
        applicationId 'de.reimardoeffinger.quickdic'
        multiDexEnabled true
        targetSdkVersion 34
        resourceConfigurations += ['de', 'es', 'eu', 'fr', 'it', 'ja', 'pt', 'pt-rBR', 'ru', 'zh-rCN']
        minSdkVersion 14
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        disable 'MissingTranslation'
    }
}
